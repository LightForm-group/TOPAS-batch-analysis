' Set TOPAS to launch mode (rocket button on the right).

' @ means refine the parameter(s).

'define report here and specify what this includes at the bottom...
#define report

' This command makes Topas calculate systematic errors ( value(xxxx)_error(yyyy) ).
do_errors

' Output of fitting quality (generally GOF < 2 is considered excellent. <3 is acceptable. <1 is beggining to over-constrain the model fitting).
r_wp  16.6040582 r_exp  31.8999004 r_p  12.8162281 r_wp_dash  17.9129788 r_p_dash  14.0051151 r_exp_dash  34.4146132 weighted_Durbin_Watson  0.890801839 gof  0.520505016

' Input file.
'macro filename {"C:\ChrisBatch\Data\adc_065_Ti64_NDload_900C_15mms_00000.xy"}
xdd filename

' Background settings - number of params is the polynomial order. When running a new fit reset to zero i.e. 0 0 0 0 0 for 5th order background.
	bkg @  0.728445288`_13.1359103 -0.457990399`_8.04855424 -0.1345379`_2.47081208  0.170034711`_0.724447817 -0.223224982`_0.220438257
	
	' Lorentz-Polarisation factor. Graphite monochromator = 26.4. Synchrotron = 90. If not then 0.
	LP_Factor( 90)
	
	' Initial background - can have large background at low scattering angles in L-XRD.
	One_on_X(@, 3.68916`_61.89546)
	
	' Delta between the actual zero position of the goniometer versus what the microswitch believes is zero. Start at zero, only refine at the end.
	Zero_Error(@, 0.01034`_0.00049)
	
	' How far off of the ideal Z height (zero position) the sample is. Start at zero, only refine at the end. Not required for SXRD, but definitely required for XRD setup.
	Specimen_Displacement(, 0)
	
	' Distances of the primary and secondary goniometer arms from the sample = 330mm for Bruker D8-Discover. 
	' Beam delivering slits in EH1 at Diamond I12 mean R-primary = 47m (47000mm) and sample-detector distance mean R-secondary = 1200mm.
	Rp 47000
	Rs 1200

	' Accounts for the umbrella effect of the diffraction cone within the Ewald sphere, where intensity can vary depending on source/detector position. Default value = 12 dont refine initially.
	Simple_Axial_Model(, 12)
	
	' Wavelengths used for the XRD source. Monochromatic only requires one set. ymin_on_ymax means ratio of peak to width, sharp peaks in SXRD mean low value of 1e-5 is appropriate. 
	' la = area (only important for poly-chromatic sources, building up by sharing out beam composition). lh and lg are Lorentz and Gaussian half-width (0.01 for SXRD is fine).
	' These settings are for Co L-XRD [insert]
	lam
		ymin_on_ymax  0.00001
		la  1 lo  0.1391986 lh  0.01 lg  0.01

	
	' Calculation resolution. Finer steps will slow things down. Larger steps can lead to square peaks.
	x_calculation_step 0.0001
	
	' Sets the extent of the fitting range. Commented out means Topas will refine the whole pattern.
	start_X 2.5
	finish_X 8.9
	
' Crystal structure (note, indent means it is part of the str).
str
	' Peak shape function. Pseudo-Voigt is fast, but not the best fit. TCHZ Thompson-Cox-Hastings psuedo-Voight is best for L-XRD and SXRD. For sycnhrotron may also use PVII (Pearson 7), but this can go wrong. 
	' Leave to refine. Note, PV_TCHZ has 4th and 6th parameter held at zero.
	TCHZ_Peak_Type(@, 0.72229`_0.16442,@,-0.05126`_0.01489,@, 0.00132`_0.00033,, 0,@, 0.17625`_0.01533,, 0)
	
	phase_name "Titanium - alpha"
	space_group "P63/mmc"
	
	' Scale governs magnitude of peak intensites based on a convolution of various structural parameters. Leave to refine.
   scale @  1.93187725e-007`_2.52e-009
   
   ' Unit cell parameters. Name is crystal strucutre. First val is a, second is c. Avoid refining with zero error and specimen displacement as these values correlate.
	Hexagonal(aa1  2.921357`_0.000315, ac1  4.667103`_0.000591)
	
  ' Atomic sites (required for a full strucutral fit). 
  ' To refine Biso (beq) factor with minimum value use "beq @ 0 min=0;" for vibrating atoms having effect on scattering factors and peak intensities.
	site Ti1     x 0.33333       y 0.66667       z 0.25000    occ Ti 1.0 beq 0 min=0;
   
   ' Calculated internally (do not appear in GUI mode).
   cell_volume  34.494`_0.009
   Phase_Density_g_on_cm3( 13.82578`_0.00346) 
   
   ' Phase fraction in weight percent.
   weight_percent wt_Ti_alpha  86.577`_1.459	
   
   ' Texture corrections. Be careful!
   ' Preferred_Orientation(@, 2.86627_0.01984,, 1 1 0) - if you know a texture strength for a given lattice plane.
	' PO_Two_Directions(@, 0.97662_0.60502,, 0 1 5,@, 0.20543_2.00591,, 0 0 8,@, 0.99493_0.16041) - if you know texture strength for more than one lattice plane.
	' PO_Spherical_Harmonics(sh_1, 2) - fitting spherical harmonics to the the texture, must define a name and a strength 2, 4 ,6 ,8 -> stronger.
	
	PO_Spherical_Harmonics(sh_1, 6 load sh_Cij_prm {
			y00   !sh_1_c00  1.00000
			y20   sh_1_c20   0.45671`_0.02650
			y40   sh_1_c40   0.43711`_0.02998
			y60   sh_1_c60   0.41978`_0.03319
			y66p  sh_1_c66p -0.17552`_0.02036
			} )


str
	TCHZ_Peak_Type(@, 0.71071`_1.34910,@,-0.09708`_0.13483,@, 0.00232`_0.00420,, 0,@, 0.81320`_0.40320,, 0)
	phase_name "Titanium-beta"
	space_group "Im-3m"
	scale @  9.51146969e-008`_1.188e-008
	Cubic(ba1  3.194093`_0.001372)
  
	volume 36.150   
	site Ti     x 0.00000       y 0.00000       z 0.00000      occ Ti 1 beq 0 min=0;
	
	cell_volume  32.587`_0.042
   Phase_Density_g_on_cm3( 4.87835`_0.00629) 
   
   weight_percent wt_Ti_beta  13.423`_1.459
   
	PO_Spherical_Harmonics(sh_2, 6 load sh_Cij_prm {
			k00   !sh_2_c00  1.00000
			k41   sh_2_c41   0.15359`_0.21122
			k61   sh_2_c61  -0.39433`_0.19272
			} )
	
#ifdef report
	out "results.txt" append
	Out_String("\n")
	Out_String(filename)
	Out(Get(r_wp), "	R_wp:\t %7.4f")
	Out(Get(gof), "	GOF:\t %7.4f")
	Out(aa1,	"	alpha_a_cell:\t %9.8f")
	Out(ac1, "	alpha_c_cell:\t %9.8f")
	Out(wt_Ti_alpha, "	alpha_wt:\t %9.8f")
	Out(ba1, "	beta_a_cell:\t %9.8f")
	Out(wt_Ti_beta, "	beta_wt:\t %9.8f")
#endif

' Error matrix to check there is no unusual correlation within the different fitting parameters.
C_matrix_normalized
{
                             1   2   3   4   5   6   7   8   9  10  11  12  13  14  15  16  17  18  19  20  21  22  23  24  25  26
bkg2e39e60             1:  100-100 100-100  95-100   2  -2  -6   4   6  -0   2  -2 -26 -17   2   2  17  -1  -3   7  -8   7   4   0
bkg2e39f00             2: -100 100-100 100 -95 100  -2   2   6  -4  -6   0  -2   1  26  17  -2  -2 -17   1   3  -7   8  -7  -5  -0
bkg2e39fa0             3:  100-100 100-100  96-100   3  -2  -6   4   6  -1   2  -2 -26 -17   2   2  17  -1  -2   7  -8   7   5   0
bkg2e3a040             4: -100 100-100 100 -95 100  -2   2   6  -3  -7  -1  -1   1  25  16  -2  -2 -17   0   2  -7   8  -7  -4  -0
bkg2e3a0e0             5:   95 -95  96 -95 100 -95   3  -1  -4   6   4   1   1  -0 -26 -19   3   2  21  -8  -1   2  -3   3   1  -0
m5d9228e2_0            6: -100 100-100 100 -95 100  -2   2   6  -4  -6   0  -2   2  26  17  -2  -2 -17   1   3  -7   8  -7  -4  -0
m5d9228e2_11           7:    2  -2   3  -2   3  -2 100   2   4  -3   0  -1   2  -3  -1  -4  88  85   0  -4  -1   3  -2   4   7  14
sh_1_c20               8:   -2   2  -2   2  -1   2   2 100  -3 -26 -15  -0   0  -1   2   7   1   1   4  -8  -1  -2   3  -4  -4  -6
sh_1_c40               9:   -6   6  -6   6  -4   6   4  -3 100  10 -19   1  -1   1  -6 -18   3   3  -3  -2   1  -0  -0   3   6  -7
sh_1_c60              10:    4  -4   4  -3   6  -4  -3 -26  10 100   9   2  -2   3  -8 -36  -1  -2  -5   9  -5   6  -6   2  -0   8
sh_1_c66p             11:    6  -6   6  -7   4  -6   0 -15 -19   9 100  -3   3  -3   4  29  -0   0   1   2   0   2  -3   3   4  -3
m5d9228e2_1           12:   -0   0  -1  -1   1   0  -1  -0   1   2  -3 100 -99  98 -26  -4  -1  -0  -2   3  -0   1  -1   1   2   0
m5d9228e2_2           13:    2  -2   2  -1   1  -2   2   0  -1  -2   3 -99 100 -99  19   1   1   2   1  -4   2  -2   2  -1  -0  -1
m5d9228e2_3           14:   -2   1  -2   1  -0   2  -3  -1   1   3  -3  98 -99 100 -22  -2  -2  -2  -2   4  -2   2  -2   1   0   2
m5d9228e2_4           15:  -26  26 -26  25 -26  26  -1   2  -6  -8   4 -26  19 -22 100  44   0  -4  12   6 -15  12  -8  -8 -23   2
scale2e97c60_         16:  -17  17 -17  16 -19  17  -4   7 -18 -36  29  -4   1  -2  44 100  -2  -4   8   5 -11   8  -4  -7 -15   6
aa1                   17:    2  -2   2  -2   3  -2  88   1   3  -1  -0  -1   1  -2   0  -2 100  66   0  -2  -2   2  -2   2   3  18
ac1                   18:    2  -2   2  -2   2  -2  85   1   3  -2   0  -0   2  -2  -4  -4  66 100  -1  -3   1   1  -2   4   8  13
sh_2_c41              19:   17 -17  17 -17  21 -17   0   4  -3  -5   1  -2   1  -2  12   8   0  -1 100 -43  -4   1   1  -4 -18  -2
sh_2_c61              20:   -1   1  -1   0  -8   1  -4  -8  -2   9   2   3  -4   4   6   5  -2  -3 -43 100 -11  45 -52  47  60  13
m5d9228e2_9           21:   -3   3  -2   2  -1   3  -1  -1   1  -5   0  -0   2  -2 -15 -11  -2   1  -4 -11 100 -63  30  22  20  -4
m5d9228e2_a           22:    7  -7   7  -7   2  -7   3  -2  -0   6   2   1  -2   2  12   8   2   1   1  45 -63 100 -93  55  21   4
m5d9228e2_b           23:   -8   8  -8   8  -3   8  -2   3  -0  -6  -3  -1   2  -2  -8  -4  -2  -2   1 -52  30 -93 100 -79 -37  -3
m5d9228e2_c           24:    7  -7   7  -7   3  -7   4  -4   3   2   3   1  -1   1  -8  -7   2   4  -4  47  22  55 -79 100  64  -4
scale2e97e40_         25:    4  -5   5  -4   1  -4   7  -4   6  -0   4   2  -0   0 -23 -15   3   8 -18  60  20  21 -37  64 100 -13
ba1                   26:    0  -0   0  -0  -0  -0  14  -6  -7   8  -3   0  -1   2   2   6  18  13  -2  13  -4   4  -3  -4 -13 100
}
