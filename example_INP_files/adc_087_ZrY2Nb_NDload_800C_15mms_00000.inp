' Set TOPAS to launch mode (rocket button on the right).

' @ means refine the parameter(s).

'define report here and specify what this includes at the bottom...
#define report

' This command makes Topas calculate systematic errors ( value(xxxx)_error(yyyy) ).
do_errors

' Output of fitting quality (generally GOF < 2 is considered excellent. <3 is acceptable. <1 is beggining to over-constrain the model fitting).
r_wp  14.7701317 r_exp  36.2216057 r_p  11.4495615 r_wp_dash  16.9530003 r_p_dash  13.3171331 r_exp_dash  41.5747744 weighted_Durbin_Watson  0.692990025 gof  0.407771312

' Input file.
'macro filename {"C:\ChrisBatch\Data\adc_087_ZrY2Nb_NDload_800C_15mms_00000.xy"}
xdd filename

' Background settings - number of params is the polynomial order. When running a new fit reset to zero i.e. 0 0 0 0 0 for 5th order background.
	bkg @  0.891852105`_10.0643589  0.0101662622`_6.29848 -0.367925999`_1.97405336  0.0567794162`_0.591226746 -0.104153117`_0.185876403
	
	' Lorentz-Polarisation factor. Graphite monochromator = 26.4. Synchrotron = 90. If not then 0.
	LP_Factor( 90)
	
	' Initial background - can have large background at low scattering angles in L-XRD.
	One_on_X(@, 8.10511`_46.26609)
	
	' Delta between the actual zero position of the goniometer versus what the microswitch believes is zero. Start at zero, only refine at the end.
	Zero_Error(@, 0.00668`_0.00041)
	
	' How far off of the ideal Z height (zero position) the sample is. Start at zero, only refine at the end. Not required for SXRD, but definitely required for XRD setup.
	Specimen_Displacement(, 0)
	
	' Distances of the primary and secondary goniometer arms from the sample = 330mm for Bruker D8-Discover. 
	' Beam delivering slits in EH1 at Diamond I12 mean R-primary = 47m (47000mm) and sample-detector distance mean R-secondary = 1500mm.
	Rp 47000
	Rs 1500

	' Accounts for the umbrella effect of the diffraction cone within the Ewald sphere, where intensity can vary depending on source/detector position. Default value = 12 dont refine initially.
	Simple_Axial_Model(, 12)
	
	' Wavelengths used for the XRD source. Monochromatic only requires one set. ymin_on_ymax means ratio of peak to width, sharp peaks in SXRD mean low value of 1e-5 is appropriate. 
	' la = area (only important for poly-chromatic sources, building up by sharing out beam composition). lh and lg are Lorentz and Gaussian half-width (0.01 for SXRD is fine).
	' These settings are for Co L-XRD [insert]
	lam
		ymin_on_ymax  0.00001
		la  1 lo  0.1391986 lh  0.01 lg  0.01

	
	' Calculation resolution. Finer steps will slow things down. Larger steps can lead to square peaks.
	x_calculation_step 0.0001
	
	' Sets the extent of the fitting range. Commented out means Topas will refine the whole pattern.
	start_X 2.4
	finish_X 8.8
	
' Crystal structure (note, indent means it is part of the str).
str
	' Peak shape function. Pseudo-Voigt is fast, but not the best fit. TCHZ Thompson-Cox-Hastings psuedo-Voight is best for L-XRD and SXRD. For sycnhrotron may also use PVII (Pearson 7), but this can go wrong. 
	' Leave to refine. Note, PV_TCHZ has 4th and 6th parameter held at zero.
	TCHZ_Peak_Type(@, -1.00000`_0.06631_LIMIT_MIN_-1,@, 0.07912`_0.00536,@, -0.00120`_0.00010,, 0,@, 0.30699`_0.01091,, 0)
	
	phase_name "Zirconium - alpha"
	space_group "P63/mmc"
	
	' Scale governs magnitude of peak intensites based on a convolution of various structural parameters. Leave to refine.
   scale @  1.92570228e-008`_2.397e-010
   
   ' Unit cell parameters. Name is crystal strucutre. First val is a, second is c. Avoid refining with zero error and specimen displacement as these values correlate.
	Hexagonal(aa1  3.226229`_0.000315, ac1  5.151231`_0.000646)
	
  ' Atomic sites (required for a full strucutral fit). 
  ' To refine Biso (beq) factor with minimum value use "beq @ 0 min=0;" for vibrating atoms having effect on scattering factors and peak intensities.
	site Zr     x 0.33333       y 0.66667       z 0.25000    occ Zr 1.0 beq 0 min=0;
   
   ' Calculated internally (do not appear in GUI mode).
   cell_volume  46.434`_0.011
   Phase_Density_g_on_cm3( 19.57392`_0.00454) 
   
   ' Phase fraction in weight percent.
   weight_percent wt_Zr_alpha  84.982`_1.404	
   
   ' Texture corrections. Be careful!
   ' Preferred_Orientation(@, 2.86627_0.01984,, 1 1 0) - if you know a texture strength for a given lattice plane.
	' PO_Two_Directions(@, 0.97662_0.60502,, 0 1 5,@, 0.20543_2.00591,, 0 0 8,@, 0.99493_0.16041) - if you know texture strength for more than one lattice plane.
	' PO_Spherical_Harmonics(sh_1, 2) - fitting spherical harmonics to the the texture, must define a name and a strength 2, 4 ,6 ,8 -> stronger.
	
	PO_Spherical_Harmonics(sh_1, 6 load sh_Cij_prm {
			y00   !sh_1_c00  1.00000
			y20   sh_1_c20  -0.30759`_0.02403
			y40   sh_1_c40  -0.07472`_0.02764
			y60   sh_1_c60   0.13780`_0.02924
			y66p  sh_1_c66p -0.38245`_0.02321
			} )


str
	TCHZ_Peak_Type(@, 0.30415`_1.64231,@, 0.04091`_0.09268,@,-0.00153`_0.00109,, 0,@, 0.43699`_0.22855,, 0)
	phase_name "Zirconium - beta"
	space_group "Im-3m"
	scale @  1.08796385e-008`_1.191e-009
	Cubic(ba1  3.518840`_0.001044)
  
	volume 45.99323   
	site Zr     x 0.00000       y 0.00000       z 0.00000      occ Zr 1 beq 0 min=0;
	
	cell_volume  43.571`_0.039
   Phase_Density_g_on_cm3( 6.95329`_0.00619) 
   
   weight_percent wt_Zr_beta  15.018`_1.404
   
	PO_Spherical_Harmonics(sh_2, 6 load sh_Cij_prm {
			k00   !sh_2_c00  1.00000
			k41   sh_2_c41   0.10996`_0.18339
			k61   sh_2_c61  -0.64282`_0.18156
			} )
	
#ifdef report
	out "results.txt" append
	Out_String("\n")
	Out_String(filename)
	Out(Get(r_wp), "	R_wp:\t %7.4f")
	Out(Get(gof), "	GOF:\t %7.4f")
	Out(aa1,	"	alpha_a_cell:\t %9.8f")
	Out(ac1, "	alpha_c_cell:\t %9.8f")
	Out(wt_Zr_alpha, "	alpha_wt:\t %9.8f")
	Out(ba1, "	beta_a_cell:\t %9.8f")
	Out(wt_Zr_beta, "	beta_wt:\t %9.8f")
#endif

' Error matrix to check there is no unusual correlation within the different fitting parameters.
C_matrix_normalized
{
                             1   2   3   4   5   6   7   8   9  10  11  12  13  14  15  16  17  18  19  20  21  22  23  24  25  26
bkg31dfe78             1:  100-100 100-100  96-100  -0   2  -9  -7  15 -14  14 -13  -9   2   1  -0   4  14  -8  11 -14  -1  -2   3
bkg31dff18             2: -100 100-100 100 -96 100   0  -2   9   7 -15  14 -14  14   9  -2  -1   0  -4 -14   8 -11  14   1   1  -3
bkg31dffb8             3:  100-100 100-100  96-100  -0   2  -9  -7  15 -14  14 -13  -9   2   1  -0   5  14  -8  11 -15  -0  -1   3
bkg31e0058             4: -100 100-100 100 -95 100   0  -2   9   7 -15  15 -14  14   9  -2  -1   0  -4 -15   8 -11  15   0   1  -3
bkg31e00f8             5:   96 -96  96 -95 100 -96   0   3  -9  -6  16 -14  13 -12  -7   3   1  -0   7  10  -6   9 -12  -1  -3   3
m5d3acf67_0            6: -100 100-100 100 -96 100   0  -2   9   7 -15  14 -14  13   9  -2  -1   0  -4 -14   8 -11  14   1   2  -3
m5d3acf67_11           7:   -0   0  -0   0   0   0 100   1   3  -3   1  -4   4  -5  -1  -5  88  70   1  -6  -4   3  -3   6   7  26
sh_1_c20               8:    2  -2   2  -2   3  -2   1 100 -27 -34   1   5  -6   5   8  18   1  -0   4  -6   3  -3   3  -4  -4  -4
sh_1_c40               9:   -9   9  -9   9  -9   9   3 -27 100 -29 -23   2  -3   3   2  -3   3   2  -0  -2  -1   0   1   4   5  -3
sh_1_c60              10:   -7   7  -7   7  -6   7  -3 -34 -29 100  13  -1   1  -0  -9 -27  -2  -2  -5   8  -0   2  -4  -4  -4   7
sh_1_c66p             11:   15 -15  15 -15  16 -15   1   1 -23  13 100  -5   5  -7   4  32  -0   0   2   2  -0   1  -1   3   6  -5
m5d3acf67_1           12:  -14  14 -14  15 -14  14  -4   5   2  -1  -5 100 -98  90  30  12  -3  -7   1   2   2  -1  -0  -5  -6  -2
m5d3acf67_2           13:   14 -14  14 -14  13 -14   4  -6  -3   1   5 -98 100 -96 -26 -12   3   8  -1  -2  -1   1   0   5   7   1
m5d3acf67_3           14:  -13  14 -13  14 -12  13  -5   5   3  -0  -7  90 -96 100  11   8  -3  -8   0   3   1  -0  -1  -4  -7   1
m5d3acf67_4           15:   -9   9  -9   9  -7   9  -1   8   2  -9   4  30 -26  11 100  42   1  -3   7   4   3  -1  -1  -9 -12   3
scale323bcf8_         16:    2  -2   2  -2   3  -2  -5  18  -3 -27  32  12 -12   8  42 100  -2  -4   7   7  -0   2  -5  -8 -10   7
aa1                   17:    1  -1   1  -1   1  -1  88   1   3  -2  -0  -3   3  -3   1  -2 100  47   2  -4  -5   4  -4   5   3  28
ac1                   18:   -0   0  -0   0  -0   0  70  -0   2  -2   0  -7   8  -8  -3  -4  47 100   0  -3  -2   2  -1   5   7  19
sh_2_c41              19:    4  -4   5  -4   7  -4   1   4  -0  -5   2   1  -1   0   7   7   2   0 100 -33  -8   7  -6   7  -4   0
sh_2_c61              20:   14 -14  14 -15  10 -14  -6  -6  -2   8   2   2  -2   3   4   7  -4  -3 -33 100 -37  43 -47  32  55   9
m5d3acf67_9           21:   -8   8  -8   8  -6   8  -4   3  -1  -0  -0   2  -1   1   3  -0  -5  -2  -8 -37 100 -98  88 -85 -37  -2
m5d3acf67_a           22:   11 -11  11 -11   9 -11   3  -3   0   2   1  -1   1  -0  -1   2   4   2   7  43 -98 100 -96  79  35   5
m5d3acf67_b           23:  -14  14 -15  15 -12  14  -3   3   1  -4  -1  -0   0  -1  -1  -5  -4  -1  -6 -47  88 -96 100 -63 -26  -9
m5d3acf67_c           24:   -1   1  -0   0  -1   1   6  -4   4  -4   3  -5   5  -4  -9  -8   5   5   7  32 -85  79 -63 100  64 -10
scale323bed8_         25:   -2   1  -1   1  -3   2   7  -4   5  -4   6  -6   7  -7 -12 -10   3   7  -4  55 -37  35 -26  64 100 -17
ba1                   26:    3  -3   3  -3   3  -3  26  -4  -3   7  -5  -2   1   1   3   7  28  19   0   9  -2   5  -9 -10 -17 100
}
